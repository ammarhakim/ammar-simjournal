load("modal-basis")$
load("basis-precalc/basisSer1x")$
load("recovery-calc/recovery")$

xL : -3*dx/2$
xR : 3*dx/2$

/* Taylor expansion */
func(z) := sum(a[i]*z^i/factorial(i),i,0,10)$

/* Function to project function on DG basis */
projectOnBasisGrid(func, bc, xL, xR, N) := block([dx],
  dx : (xR-xL)/N,
  return (
    makelist(
      calcInnerProdList([x],1,bc,
        subst(z=x*dx/2+(xL+(i-1/2)*dx), apply(func, [z]))),
      i, 1, N)
    )
  )$

/* Compute remainder polynomial of expansion. Second optional argument
* is order of continuity at cell edges */
calcRemPoly(polyOrder, [C]) := block(
  [bc, taylorProj, h, cmax],
  bc : basisC[polyOrder],
  cmax : if (length(C) > 0) then C[1] else 1,
  /* Project Taylor expansion on basis */
  taylorProj : projectOnBasisGrid(func, bc, xL, xR, 3),
  /* Contruct recovery polynomial */
  h : getRecov3CellGen(x, [dx], polyOrder, cmax, taylorProj[1], taylorProj[2], taylorProj[3]),
  return (expand(h-func(x)))
  )$