/* Poisson solver based on recovery approach */

load("modal-basis")$
load("basis-precalc/basisSer1x")$
load("recovery")$

/* Function to make agruments to draw2d() function */
createArgListDgSol(dgField, clr, xL, xR) := block([dx,argList,xj,N],
  N : length(dgField),
  dx : (xR-xL)/N,
  argList : [grid=true, color=clr],
  for i : 1 thru N do (
    xj : xL+(i-1/2)*dx,
    argList : endcons(explicit(
        dgField[i].subst(x=(z-xj)/(dx/2),bc),z,xL+(i-1)*dx,xL+i*dx),
      argList)
    ),
  return(argList)
  )$

__etat(x,a,b) := (x-(b+a)/2)/((b-a)/2)$

/* Function to create list of recovered polynomials on grid given DG representation */
calcRecoveredPolySol(dgField, xL, xR, bcLeft, bcRight) := block([dx,ncells,polyOrder,polyList],
  ncells : length(dgField),
  dx : (xR-xL)/ncells,
  polyOrder : length(dgField[1])-1,
  polyList : [],
  /* Left cell */
  polyList : endcons(
    subst(x=__etat(z,xL,xL+dx),
      calcRecov1CellGen("Ser", x, [2], polyOrder, 1, ["pt", bcLeft], dgField[1], ["exp", dgField[2]])
      ),
    polyList
    ),
  /* Interior cells */
  for i : 2 thru ncells-1 do (
  polyList : endcons(
    subst(x=__etat(z,xL+(i-1)*dx,xL+i*dx),
      calcRecov1CellGen("Ser", x, [2], polyOrder, 1, ["exp", dgField[i-1]], dgField[i], ["exp", dgField[i+1]])
      ),
    polyList
    )
  ),
  /* Right cell */
  polyList : endcons(
    subst(x=__etat(z,xR-dx,xR),
      calcRecov1CellGen("Ser", x, [2], polyOrder, 1, ["exp", dgField[ncells-1]], dgField[ncells], ["pt", bcRight])
      ),
    polyList
    ),  
  return(polyList)
  )$

/* Function to take list of expressions and construct draw2d command parameters */
createArgListRecovSol(dgField, clr, xL, xR, bcLeft, bcRight) := block([dx,ncells,rpoly,argList],
  ncells : length(dgField),
  dx : (xR-xL)/ncells,
  argList : [],
  rpoly : calcRecoveredPolySol(dgField, xL, xR, bcLeft, bcRight),
  return (
    append([color=clr,line_type=dots],
      makelist(
        explicit(rpoly[i],z,xL+(i-1)*dx,xL+i*dx),i,1,length(rpoly)
        )
      )
    )
  )$

/* Function to project function on DG basis */
projectOnBasisGrid(func, bc, xL, xR, N) := block([dx],
  dx : (xR-xL)/N,
  return (
    makelist(
      calcInnerProdList([x],1,bc,
        subst(z=x*dx/2+(xL+(i-1/2)*dx), apply(func, [z]))),
      i, 1, N)
    )
  )$

/* Function to subtract value from first element in list of list */
subVal(dgField, val) := block([outList],
  outList : copylist(dgField),
  for i : 1 thru length(dgField) do (
    outList[i][1] : dgField[i][1]-val
    ),
  return(outList)
  )$

/* Function to compute L2 norm of field */
calcL2Norm(dgField, dx) := sqrt(dx*sum(sum(dgField[i][k]^2,k,1,length(dgField[1])), i, 1, length(dgField)))$

/* Function to compute gradient stencil in 1D for interior cell */
getGradStencil(polyOrder, fL, fC, fR) :=  calcInnerProdList([x], 1,
  basisC[polyOrder],
  diff(calcRecov1CellGen("Ser", x, [2], polyOrder, 1, ["exp", fL], fC, ["exp", fR]), x,1)
  )$

/* Function to compute Laplacian stencil in 1D for interior cell */
getLaplacianStencil(polyOrder, fL, fC, fR) :=  calcInnerProdList([x], 1,
  basisC[polyOrder],
  diff(calcRecov1CellGen("Ser", x, [2], polyOrder, 1, ["exp", fL], fC, ["exp", fR]), x,2)
  )$
/* Function to compute Laplacian stencil in left-skin 1D for interior cell */
getLaplacianLeftSkinStencil(polyOrder, fL, fC, fR) :=  calcInnerProdList([x], 1,
  basisC[polyOrder],
  diff(calcRecov1CellGen("Ser", x, [2], polyOrder, 1, ["pt", fL], fC, ["exp", fR]), x,2)
  )$
/* Function to compute Laplacian stencil in right-skin 1D for interior cell */
getLaplacianRightSkinStencil(polyOrder, fL, fC, fR) :=  calcInnerProdList([x], 1,
  basisC[polyOrder],
  diff(calcRecov1CellGen("Ser", x, [2], polyOrder, 1, ["exp", fL], fC, ["pt", fR]), x,2)
  )$

/* Transform to [-1,1] */
xe(eta,dx,xj)  := eta*dx/2+xj$

/*
* grid: grid(lower=xl, upper=xr,cells=N,isPeriodic=false)
*
*    isPeriodic is optional
*
* bcLeft/bcRight: BC(type=1, value=0)
*
*    type can be 1 (Dirichlet) or 2 (Neumann). Value muust be a number
*/
solvePoissonEquation(grid, polyOrder, srcFunc, bcLeft, bcRight) := block(
  /* Declare local variables */
  [
  bc, NC, xL, xR, dx, nmax, slist, stencil, eqnList,
  varList, fsol, isPeriodic,
  bcType, bcValue, bcLeftList, bcRightList,
  srcMean
  ],
  
  bc : basisC[polyOrder],
  NC : length(bc),
  xL : assoc(lower,grid),
  xR : assoc(upper,grid),
  nmax : assoc(cells,grid),
  dx : (xR-xL)/nmax, /* Cell spacing */
  
  /* Source function on [-1,1] */
  srcFuncHat(eta,dx,xj) := srcFunc(xe(eta,dx,xj)),
  /* Check if periodic direction */
  isPeriodic : assoc(isPeriodic,grid),
  /* Substitution list for indexing ... */
  slist : append(
    /* x-direction for f ... */
    makelist(fL[k]=f[i-1,k],k,1,NC),
    makelist(fC[k]=f[i,k],k,1,NC),
    makelist(fR[k]=f[i+1,k],k,1,NC)
    ),
  /* ... for periodic BCs */
  pBc : append(
    /* for f ... */
    makelist(f[0,k]=f[nmax,k],k,1,NC),
    makelist(f[nmax+1,k]=f[1,k],k,1,NC)
    ),
  
  /* Construct list of equations */
  if isPeriodic then (
    srcMean : integrate(srcFunc(x),x,xL,xR)/(xR-xL),
    /* Interior stencil */
    stencil : subst(slist,
      getLaplacianStencil(polyOrder, fL, fC, fR)
      ),
    eqnList : [], /* List of equations */
    /* Construct set of linear equations for interior */
    for j : 1 thru nmax do (
      eqnList : append(eqnList,
        4/dx^2*subst(i=j,stencil)-calcInnerProdList([x],1,bc,-(srcFuncHat(x,dx,xL+(j-1/2)*dx)-srcMean))
        )
      ),
    /* Apply periodic BCs */
    eqnList : subst(pBc, eqnList),
    /* Set cell-average term in first skin cell to zero to avoid singular inversion */
    eqnList : rest(append(eqnList, [f[1,1]=0])) /* remove one equation */
    )
  else (
    /* Create left/right BC lists (also needed for skin/first-interior cells) */
    bcType : assoc(type,bcLeft),
    bcValue : assoc(value,bcLeft),
    bcLeftList : if bcType = 1 then [1,0,bcValue] else [0,1,bcValue],
    
    bcType : assoc(type,bcRight),
    bcValue : assoc(value,bcRight),
    bcRightList : if bcType = 1 then [1,0,bcValue] else [0,1,bcValue],

    eqnList : [], /* List of equations */
    /* Interior stencil */
    stencil : subst(slist,
      getLaplacianStencil(polyOrder, fL, fC, fR)
      ),
    /* Construct set of linear equations for interior */
    for j : 2 thru nmax-1 do (
      eqnList : append(eqnList,
        4/dx^2*subst(i=j,stencil)-calcInnerProdList([x],1,bc,-srcFuncHat(x,dx,xL+(j-1/2)*dx))
        )
      ),
    
    /* Left skin cell */
    stencil : subst(slist,
      getLaplacianLeftSkinStencil(polyOrder, bcLeftList, fC, fR)
      ),
    eqnList : append(eqnList,
      4/dx^2*subst(i=1,stencil)-calcInnerProdList([x],1,bc,-srcFuncHat(x,dx,xL+(1-1/2)*dx))
      ),
    
    /* Right skin cell */
    stencil : subst(slist,
      getLaplacianRightSkinStencil(polyOrder, fL, fC, bcRightList)
      ),
    eqnList : append(eqnList,
      4/dx^2*subst(i=nmax,stencil)-calcInnerProdList([x],1,bc,-srcFuncHat(x,dx,xL+(nmax-1/2)*dx))
      )
    ),
  
  /* List of unknowns */
  varList : [],
  for i : 1 thru nmax do (
    varList : append(varList, makelist(f[i,k],k,1,NC))
    ),  
  /* Return solution field */
  /* Solve linear system */
  fSol : linsolve(eqnList, varList),
  /* Return solution field */
  return (
    subst(fSol, makelist(makelist(f[i,k],k,1,NC), i, 1, nmax))
    )
  )$




